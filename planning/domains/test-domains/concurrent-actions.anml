// -*-c++-*-

/**
 * This is an example for concurrent actions to open a door.
 * To open the door, we have to perform an action Unlock
 * inside another action Open.
 */
type Door with {
  variable boolean locked;
  variable boolean opened;
}; 

action Unlock(Door d) {
  duration := 10;

  [start,start+1] d.locked == true :-> false;
  [end-1,end] d.locked == false :-> true;
};

action Open(Door d) {
  duration := 20;

  [start] { d.locked == true; };
  [end]   { d.locked == true; };

  [all] contains d.locked == false;
  [all] d.opened == false :-> true;
};

instance Door d;

[start] { 
  d.locked := true;
  d.opened := false;
};

[end] {
  d.opened == true;
};

